plugins {
    id 'java'
    id 'maven-publish'
    id 'xyz.jpenilla.run-paper' version '2.1.0'
    id 'io.papermc.paperweight.userdev' version '1.5.3'
    id 'com.github.breadmoirai.github-release' version '2.4.1'
}

group = 'com.github.SimpleClientDevelopment'
version = '0.1.3'

repositories {
}

dependencies {
    paperweight.paperDevBundle '1.19.4-R0.1-SNAPSHOT'
}

processResources {
    filesMatching('plugin.yml') {
        expand 'version': version
    }
}

tasks {
    runServer {
        minecraftVersion '1.19.4'
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set 17
}

githubRelease {
    token System.getenv('GITHUB_TOKEN') == null ? '' : System.getenv('GITHUB_TOKEN')
    owner 'SimpleClientDevelopment'
    repo 'SimpleClientAPI'
    tagName version
    targetCommitish 'main'
    releaseName version
    generateReleaseNotes false
    body file('changelog.md').text.replace('%version%', version)
    draft false
    prerelease version.startsWith('0') || version.endsWith('-snapshot') || version.endsWith('-alpha') || version.endsWith('-beta') || version.endsWith('-dev')
    releaseAssets files("build/libs/${archivesBaseName}-${version}.jar")
    allowUploadToExisting.set false
    overwrite true
    dryRun false
    apiEndpoint 'https://api.github.com'
    client
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}